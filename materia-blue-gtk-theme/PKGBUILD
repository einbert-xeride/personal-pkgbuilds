# Upstream Maintainer: Bruno Pagani <archange@archlinux.org>
# Upstream Contributor: cth451 <cth451@gmail.com>

pkgname=materia-blue-gtk-theme
_pkgname=materia-gtk-theme
pkgver=20190201
pkgrel=1
pkgdesc="A Material Design theme for GNOME/GTK+ based desktop environments, blue accent patch"
arch=('any')
url="https://github.com/nana-4/materia-theme"
license=('GPL')
depends=('gtk3' 'gnome-themes-extra')
optdepends=('gtk-engine-murrine: for gtk2 theme')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/nana-4/materia-theme/archive/v${pkgver}.tar.gz")
sha256sums=('f483894a5519cbc5e7e208047f86550d29291a7248372f045950a6849e2c1eb9')

package() {
    cd ${_pkgname/gtk-/}-${pkgver}
    sed -i "s/Materia/MaterialBlue/g" install.sh
    sed -i "/Comment/!s/Materia/MaterialBlue/g" src/*.theme
    find src -type f -print -exec sed -i -e 's/#01A299/#448AFF/g' {} \;
    ./parse-sass.sh
    ./render-assets.sh
    install -d "${pkgdir}"/usr/share/themes
    ./install.sh -d "${pkgdir}"/usr/share/themes
}
